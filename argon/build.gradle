apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: '../quality/quality.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    
    buildTypes {
        libraryVariants.all { variant ->
            task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
                description "Generates Javadoc for $variant.name."
                source = variant.javaCompile.source
                destinationDir = file("../javadoc/")
                ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
                classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
                options {
                    links "http://docs.oracle.com/javase/7/docs/api/"
                    linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
                }
                options.addStringOption('Xdoclint:none', '-quiet')
                exclude '**/BuildConfig.java'
                exclude '**/R.java'
            }
        }
    }
}

dependencies {
    compile 'com.jakewharton:process-phoenix:1.0.2'
    compile 'com.google.code.gson:gson:2.5'
    
    compile 'com.android.support:appcompat-v7:23.1.1'
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = 'de.sevenfactory'
        pom.artifactId = project.name
        pom.version = android.defaultConfig.versionName

        repository(url: 'file://maven')
    }
}

